PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
      ?bestuurseenheid
        ext:kbonummer ?kboNummer;
        dcterms:identifier ?kboNummer.
    }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/landing-zone/op-public> {
    ?identifier
      skos:notation "KBO nummer";
      generiek:gestructureerdeIdentificator ?gestructureerdeIdentificator.

    ?gestructureerdeIdentificator
      generiek:lokaleIdentificator ?kboNummer.

    ?bestuurseenheid
      adms:identifier ?identifier.
  }
}

;

INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
      ?bestuurseenheid
        dcterms:identifier ?ovoNummer.
    }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/landing-zone/op-public> {
    ?identifier
      skos:notation "OVO-nummer";
      generiek:gestructureerdeIdentificator ?gestructureerdeIdentificator.

    ?gestructureerdeIdentificator
      generiek:lokaleIdentificator ?ovoNummer.

    ?bestuurseenheid
      adms:identifier ?identifier.
  }
}